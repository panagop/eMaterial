"Έλεγχος περίσφιξης υποστυλωμάτων για ΚΠΜ <hr/><small>Σύμφωνα με τον Ευρωκώδικα 8</small>"

"<h2>Δεδομένα</h2>"

"<h3>Διαστάσεις υποστυλώματος</h3>"
b_c = ? {400}mm
h_c = ? {1000}mm

"<h3>Υλικά</h3>"
'Θλιπτική αντοχή σκυροδέματος: 'f_ck = ? {20}MPa
'Όριο διαρροής χάλυβα: 'f_yk = ? {500}MPa
'Μέτρο ελαστικότητας χάλυβα: 'E_s = ? {200}GPa

a_cc = ? {1}
'Συντελεστής ασφάλειας σκυροδέματος: 'γ_c = ? {1.5}
'Συντελεστής ασφάλειας χάλυβα: 'γ_s = ? {1.15}

"<h3>Οπλισμός</h3>"
'Ονομαστική τιμή της επικάλυψης: 'c_nom = ? {15}mm
'Διάμετρος διαμήκων ράβδων: 'ø_L = ? {18}mm
'Διάμετρος συνδετήρων: 'ø_w = ? {8}mm
'Αποστάσεις συνδετήρων: 's = ? {110}mm

'Αρθιμός διαμήκων ράβδων στην πλευρά b: 'n_L,b = ? {6}
'Αρθιμός διαμήκων ράβδων στην πλευρά h: 'n_L,h = ? {6}

'Αρθιμός σκελών συνδετήρων στην πλευρά b: 'n_w,b = ? {4}
'Αρθιμός σκελών συνδετήρων στην πλευρά h: 'n_w,h = ? {6}

'Όγκος κλειστών συνδετήρων: 'V_w = ? {0}mm^3
'Αν δεν οριστεί τιμή από τον χρήστη, λαμβάνεται προσεγγιστικά και προς την πλευρά της ασφάλειας, ο όγκος των σκελών των συνδετήρων. Αγνοείται έτσι το μήκος των εσωτερικών συνδετήρων που βρίσκεται στις παρειές, λαμβάνονται όμως υπόψη και πιθανοί σιγμοειδείς συνδετήρες.'

"<h3>Εντατικά μεγέθη</h3>"
N_d = ? {270}kN

"<h3>Πλαστιμότητα</h3>"
'Πλαστιμότητα καμπυλωτήτων: 'μ_φ = ? {10.2}

"<h2>Αποτελέσματα</h2>"

"<h3>Αντοχές σχεδισμού υλικών</h3>"
f_cd = f_ck*a_cc/γ_c
f_yd = f_yk/γ_s

"<h3>Διαστάσεις</h3>"
"<h4>Στατικό ύψος</h4>
d_1 = c_nom + ø_w + 0.5*ø_L
'Στατικό ύψος: 'd = h_c - d_1

"<h4>Διαστάσεις πυρήνα</h4>"
b_o = b_c - 2*(c_nom + 0.5*ø_w)
h_o = h_c - 2*(c_nom + 0.5*ø_w)

"<h3>Εμβαδό ράβδου (ενός) συνδετήρα</h3>"
A_sw1 = π*ø_w^2/4

"<h3>Ελάχιστος επιτρεπόμενος αριθμός ράβδων ανά παρειά</h3>"
'Υπολογίζεται βάσει της μέγιστης επιτρεπόμενης απόστασης μεταξύ ράβδων που συγκρατώνται από συνδετήρες, 200mm για ΚΠΜ.'
"<h4>Πλευρά b</h4>
n_L,min_b = ceiling(1 + (b_c - 2*d_1)/200mm)
#if n_L,b ≥ n_L,min_b
	'Υπάρχει επαρκής αριθμός διαμήκων ράβδων στην πλευρά b'
#else
	'Δεν υπάρχει επαρκής αριθμός διαμήκων ράβδων στην πλευρά b'"
#end if
"<h4>Πλευρά h</h4>
n_L,min_h = ceiling(1 + (h_c - 2*d_1)/200mm)
#if n_L,h ≥ n_L,min_h
	'Υπάρχει επαρκής αριθμός διαμήκων ράβδων στην πλευρά h'
#else
	'Δεν υπάρχει επαρκής αριθμός διαμήκων ράβδων στην πλευρά h'"
#end if

"<h3>Όγκος κλειστών συνδετήρων</h3>"
#if V_w > 0mm^3
	'Λαμβάνεται υπόψη η τιμή που ορίστηκε από τον χρήστη'V_w
#else
	'Δεν ορίστηκε τιμή από τον χρήστη, οπότε υπολογίζεται προσεγγιστικά'
	Σl_i = n_w,b*b_o + n_w,h*h_o
	V_w = Σl_i*A_sw1
#end if

"<h3>Όγκος σκυροδέματος πυρήνα</h3>"
V_o = b_o*h_o*s

"<h3>Ογκομετρικό ποσοστό συνδετήρων</h3>"
ω_wd = (V_w/V_o)*(f_yd/f_cd)

#if ω_wd ≥ 0.08
	'Έλεγχος 'ω_wd'&ge;0.08. Ο έλεγχος ικανοποιείται'
#else
	'Έλεγχος 'ω_wd'&ge;0.08. Ο έλεγχος δεν ικανοποιείται'
#end if

"<h3>Συντελεστής αποδοτικότητας της περίσφιξης</h3>"
'Υπολογισμός των αποστάσεων b<sub>i</sub> σε κάθε παρειά'
b_i_b = b_o/(n_w,b - 1)
b_i_h = h_o/(n_w,h - 1)

Σb2_i = 2*((n_w,b - 1)*b_i_b^2 + (n_w,h - 1)*b_i_h^2)

α_n = 1 - Σb2_i/(6*b_o*h_o)
α_s = (1 - s/(2*b_o))*(1 - s/(2*h_o))
α = α_n*α_s

"<h3>Έλεγχος</h3>"

v_d = N_d/(b_c*h_c*f_cd)
ε_sy,d = f_yd/E_s
30*μ_φ*v_d*ε_sy,d*(b_c/b_o) - 0.035

α*ω_wd
'<p class="ref">[EC8 § 5.15]</p>

#if α*ω_wd ≥ 30*μ_φ*v_d*ε_sy,d*(b_c/b_o) - 0.035
	'Ο έλεγχος ικανοποιείται'
#else
	'Ο έλεγχος δεν ικανοποιείται'
#end if





